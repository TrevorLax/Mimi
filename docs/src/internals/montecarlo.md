# Monte Carlo Simulation support

## Overview

Monte Carlo Simulation support consists of three primary user-facing elements:

1. The `@defmcs` macro, which defines random variables (RVs) which are assigned distributions and associated with model parameters, and

2. The optional `generate_trials!` function, which can be used to pre-generate all trial data, save all random variable values in a file, and/or override the default (Latin Hypercube) sampling method.

3. The `run_mcs` function, which runs a simulation, with parameters describing the number of trials and optional callback functions to customize simulation behavior.

These are described further below.

## The @defmcs macro

Monte Carlo Simulations are defined using the macro ```@defmcs```, which does the following:

* Defines random variables (RV) by assigning names to distributions, which can be any object that supports the following function:
  
    `rand(dist, count::Int=1)`

  which produces a single value when `count == 1`, else a `Vector` of values.
  
  If using Latin Hypercube Sampling (LHS) is used, the following function must also be defined:

  `quantile(dist, quantiles::Vector{Float64})`
  
  which returns values for the given `quantiles` of the distribution.

  In addition to the distributions available in the `Distributions` package, Mimi provides:

  * `EmpiricalDistribution`, which takes a vector of values and (optional) vector of probabilities and produces samples from these values using the given probabilities, if provided, or equal probability otherwise.

  * `SampleStore{T}`, which stores a vector of samples that are produced in order by the `rand` function. This allows the user to to store a predefined set of values (useful for regression testing) and it is used by the LHS method, which draws all required samples at once at equal probability intervals and then shuffles the values. It is also used when rank correlations are specified, since this requires re-ordering draws from random variables.

* Defines how to apply the values generated by each RV to model parameters based on a pseudo-assignment operator:
  * ```param = RV``` replaces the values in the parameter with the value of the RV for the current trial.
  * ```param += RV``` replaces the values in the parameter with the sum of the original value and the value of the RV for the current trial.
  * ```param *= RV``` replaces the values in the parameter with the product of the original value and the value of the RV for the current trial.

* Defines desired rank correlations between pairs of random variables. Approximate rank correlation is achieved by re-ordering vectors of random draws as per Iman and Conover (1982).

The ```@defmcs``` macro returns a ```MonteCarloSimulation``` instance, which contains all the definition information in a form that can be applied at run-time.

## The generate_trials! function

The `generate_trials!` function can be used to pre-generate data for the given number of `trials`, save all random variable values in the file `filename`, and/or override the default sampling method. Its calling signature is:

```
generate_trials!(mcs::MonteCarloSimulation, trials::Int; 
                 filename::String="", sampling::SamplingOptions=RANDOM)
```

If the `mcs` parameter has multiple scenarios and the `scenario_loop` placement is set to `OUTER`, this function must be called if the user wants to ensure the same trial data be used in each scenario. If this function is not called, new trial data will be generated for each scenario. 

The `sampling` parameter is of type `SamplingOptions`, an `enum` with two values, `LHS`, and `RANDOM`. Latin Hypercube sampling divides the distribution into equally-spaced quantiles, obtains values at those quantiles, and then shuffles the values. The result is better representation of the tails of the distribution with fewer samples than would be required for purely random sampling.

Note that in the current implementation, rank correlation between parameters is supported only for the `LHS` option.

Also note that if the `filename` argument is used, all random variable draws are saved to the given filename. Internally, any `Distribution` instance is converted to a `SampleStore` and the values are subsequently returned in the order generated when `rand!` is called.

If this function is not called prior to calling `run_mcs`, random sampling is used for all distributions and trial data are not saved.

## Assigning distributions

In `@defmcs`, you can apply distributions to specific slices of array parameters,
and you can "bulk assign" distributions to elements of a vector or matrix
using a more condensed syntax.

### Assigning to array slices

Options for applying distributions to array slices is accomplished using
array access syntax on the left-hand side of an assignment. The assignment
may use any of these assignment operators: `=`, `*=`, or `+=`, as described
above. Slices can be indicated using a variety of specifications. Assume we
define two parameters in `@defcomp` as
```
  foo = Parameter(index=[regions])
  bar = Parameter(index=[time, regions])
```
with `regions` defined as `[:USA, :CAN, :MEX, :ROW]`

We can assign distributions to the elements of `foo` several ways:

* Using a symbol or string or tuple of symbols or strings. Note that values specified without a ":" prefix or double quotes are treated as symbols. To specify strings, quote them the usual way.
  * `foo[USA] = Uniform(0, 1)` would assign the RV to `foo[:USA]` only.
  * `foo[(USA, CAN, MEX)] = Uniform(0, 1)` would assign the same RV to 3 elements of `foo`. 
    That is, a single value is drawn from the RV with distribution `Uniform(0, 1)` and this
    value is assigned to all three elements of `foo`.
* A ":", indicating all elements for this dimension
  * `foo[:] = Normal(10.0 3.0)` would use a draw from the Normal RV for all elements of `foo`.
* A ":" range, with or without a step, or a tuple of integers
  * `bar[2050:10:2080, :] = Uniform(2, 3)` would assign a single Uniform RV to all regions for 
    time steps with labels 2050, 2060, 2070, and 2080.
  * `bar[(2050, 2060, 2070, 2080), :] = Uniform(2, 3)` does the same thing using a tuple of values.

If `regions` were defined using strings, as in `["USA", "CAN", "MEX", "ROW"]`, the examples above would be
written as `foo["USA"] = Uniform(0, 1)` and so on.

### Assigning a vector of distributions

In some cases, it's more convenient to assign a vector of distributions (e.g., with different functional forms or parameters) to a single parameter. For example we can use the following syntax:

```
  foo = [USA => Uniform(0, 1),
         (CAN, MEX) => Uniform(1, 2),
         ROW => Normal(10, 3)]
```

which is equivalent to:

```
  foo[USA] = Uniform(0, 1),
  foo[(CAN, MEX)] = Uniform(1, 2),
  foo[ROW] = Normal(10, 3)]
```

To assign to parameters with more than one dimension, use square brackets around the dimensions
on the left-hand side of each `=>` operator, e.g.,

```
  bar = [[2050, USA] => Uniform(0, 1),
         [:, (CAN, MEX)] => Uniform(1, 2),
         [2010:10:2080, ROW] => Normal(10, 3)]
```

Currently, the more condensed syntax (using the pair operator `=>`) supports only direct assignment 
of RV value, i.e., you cannot combine this with the `*=` or `+=` operators.

## The run_mcs function

In it's simplest use, the `run_mcs` function iterates over a given number of trials, perturbing a chosen set of Mimi's "external parameters", based on the defined distributions, and then runs the given Mimi model. Optionally, trial values and/or model results are saved to CSV files.

### Function signature

The full signature for the `run_mcs` is:

```
function run_mcs(mcs::MonteCarloSimulation, 
                 trials::Union{Int, Vector{Int}, AbstractRange{Int}},
                 models_to_run::Int=length(mcs.models);
                 ntimesteps::Int=typemax(Int), 
                 output_dir::Union{Nothing, AbstractString}=nothing, 
                 pre_trial_func::Union{Nothing, Function}=nothing, 
                 post_trial_func::Union{Nothing, Function}=nothing,
                 scenario_func::Union{Nothing, Function}=nothing,
                 scenario_placement::ScenarioLoopPlacement=OUTER,
                 scenario_args=nothing)
```

The `run_mcs` function runs the indicated trial numbers, where the first `models_to_run` 
associated models are each run for `ntimesteps`, if specified, else to the maximum defined time period. 
Note that trial data are applied to all the associated models even when running only a portion of them.
    
If `pre_trial_func` or `post_trial_func` are defined, the designated functions are called 
just before or after (respectively) running a trial. The functions must have the signature:

    fn(mcs::MonteCarloSimulation, trialnum::Int, ntimesteps::Int, tup::Tuple)

where `tup` is a tuple of scenario arguments representing one element in the cross-product
of all scenario value vectors. In situations in which you want the MCS loop to run only
some of the models, the remainder of the runs can be handled using a `pre_trial_func` or
`post_trial_func`.

If provided, `scenario_args` must be a `Vector{Pair}`, where each `Pair` is a symbol and a 
`Vector` of arbitrary values that will be meaningful to `scenario_func`, which must have
the signature:

    scenario_func(mcs::MonteCarloSimulation, tup::Tuple)

By default, the scenario loop encloses the Monte Carlo loop, but the scenario loop can be
placed inside the Monte Carlo loop by specifying `scenario_placement=INNER`. When `INNER` 
is specified, the `scenario_func` is called after any `pre_trial_func` but before the model
is run.

### Associating models with a MonteCarloSimulation

The "core" `run_mcs` function assumes the `MonteCarloSimulation` instance has references to the model or models to run. There are several methods for associating the model(s) to run with the `MonteCarloSimulation` instance. The first involves calling one of the following functions:

```
set_models!(mcs::MonteCarloSimulation, models::Vector{Model})

set_model!(mcs::MonteCarloSimulation, m::Model)

set_model!(mcs::MonteCarloSimulation, mm::MarginalModel)
```

The other approach is to call a variant of `run_mcs` that takes a model or vector of models as an argument:

```
run_mcs(mcs::MonteCarloSimulation, m::Model, trials=mcs.trials; kwargs...)

run_mcs(mcs::MonteCarloSimulation, models::Vector{Model}, trials=mcs.trials; kwargs...)
```

These are simply convenience functions that call `set_model!()` or `set_models!()` prior to calling the core `run_mcs` function.

### Non-stochastic Scenarios

In many cases, scenarios (which we define as a choice of values from a discrete set for one or more parameters) need to be considered in addition to the stochastic parameter variation. To support scenarios, `run_mcs` also offers iteration over discrete scenario values, which are passed to `run_mcs` via the keyword parameter `scenario_args::Dict{Symbol, Vector}`. For example, to iterate over scenario values "a", and "b", as well as, say, discount rates `0.025, 0.05, 0.07`, you could provide the argument:

`scenario_args=Dict([:name => ["a", "b"], :rate => [0.025, 0.05, 0.07]])`

Of course, the MCS subsystem does not know what you want to do with these values, so the user must also provide a callback function in the `scenario_func` argument. This function must be defined with the signature:

`function any_name_you_like(mcs::MonteCarloSimulation, tup)`

where `tup` is an element of the set of tuples produced by calling `Itertools.product()` on all the scenario arguments. In the example above, this would produce the following vector of tuples:

`[("a", 0.025), ("b", 0.025), ("a", 0.03), ("b", 0.03), ("a", 0.05), ("b", 0.05)]`.

This approach allows all scenario combinations to be iterated over using a single loop. A final keyword argument, `scenario_placement::ScenarioLoopPlacement` indicates whether the scenario loop should occur inside or outside the loop over stochastic trial values. The type `ScenarioLoopPlacement` is an `enum` with values `INNER` and `OUTER`, the latter being the default placement.

In approximate pseudo-julia, these options produce the following behavior:

*scenario_placement=OUTER*
```
for tup in scenario_tuples
  scenario_func(tup)

  # for each scenario, run all MCS trials
  for trial in trials
    trial_data = get_trial_data(trial)
    apply_trial_data()

    pre_trial_func()
    run(model)
    post_trial_func()
  end
end
```

*scenario_placement=INNER*
```   
for trial in trials
  trial_data = get_trial_data(trial)
  apply_trial_data()

  # for each MCS trial, run all scenarios
  for tup in scenario_tuples
    scenario_func(tup)

    pre_trial_func()
    run(model)
    post_trial_func()
  end
end
```

### Running Multiple Models

In some simulations, a baseline model needs to be compared to one or more models that are perturbed parametrically or structurally (i.e., with different components or equations.) To support this, the `MonteCarloSimulation` type holds a vector of `Model` instances, and allows the caller to specify how many of these to run automatically for each trial. Note that regardless of how many models are run, the random variables are applied to all of the models associated with the simulation.

By default, all defined models are run. In some cases, you may want to run some of the models "manually" in the `pre_trial_func` or `post_trial_func`, which allow you to make arbitrary modifications to these additional models.

**Example**

The following example is available in `"Mimi.jl/test/mcs/test_defmcs.jl"`.

```julia
using Mimi
using Distributions

include("examples/tutorial/02-two-region-model/main.jl")

m = model # defined by 2-region model

mcs = @defmcs begin
    # Define random variables. The rv() is required to disambiguate an
    # RV definition name = Dist(args...) from application of a distribution
    # to an external parameter. This makes the (less common) naming of an
    # RV slightly more burdensome, but it's only required when defining
    # correlations or sharing an RV across parameters.
    rv(name1) = Normal(1, 0.2)
    rv(name2) = Uniform(0.75, 1.25)
    rv(name3) = LogNormal(20, 4)

    # define correlations
    name1:name2 = 0.7
    name1:name3 = 0.5

    # assign RVs to model Parameters
    share = Uniform(0.2, 0.8)
    sigma[:, Region1] *= name2
    sigma[2020:5:2050, (Region2, Region3)] *= Uniform(0.8, 1.2)

    # Assign an array of distributions, keyed by region, to parameter depk
    depk = [Region1 => Uniform(0.7, 1.3),
            Region2 => Uniform(0.8, 1.2),
            Region3 => Normal()]

    # indicate which parameters to save for each model run. Specify
    # a parameter name or [later] some slice of its data, similar to the
    # assignment of RVs, above.
    save(grosseconomy.K, grosseconomy.YGROSS, 
         emissions.E, emissions.E_Global)
end

# Optional user functions can be called just before or after a trial is run
function print_result(m::Model, mcs::MonteCarloSimulation, trialnum::Int)
    ci = Mimi.compinstance(m.mi, :emissions)
    value = Mimi.get_variable_value(ci, :E_Global)
    println("$(ci.comp_id).E_Global: $value")
end

# Generate trial data for all RVs and (optionally) save to a file
generate_trials!(mcs, 1000, filename="/tmp/trialdata.csv")

# Run trials 1:4, and save results to the indicated directory, one CSV file per RV
run_mcs(m, mcs, 4, output_dir="/tmp/Mimi")

# Same thing but with a post-trial function
run_mcs(m, mcs, 4, post_trial_func=print_result, output_dir="/tmp/Mimi")
```


# Not Implemented (Yet)

The remaining sections describe an API that hasn't been developed yet.

## ModelRunner


There are several types of analyses that require an ensemble of model runs performed over a set of parameter values. These include traditional Monte Carlo simulation, in which random values are drawn from distributions and applied to model parameters, as well as global sensitivity analyses that use prescribed methods for defining trial data (e.g., Sobol sampling), and Markov Chain Monte Carlo, which computes new trial data based on prior model results.

The abstract type ```ModelRunner``` will be used to define a generic API for model runners, including support for parallelization of analyses on multiprocessors and cluster computing systems. The specific sampling and analysis methods required for each subtype of ```ModelRunner``` will be defined in the concrete subtype, e.g., ```MonteCarloRunner```.

The generic process will look something like this:

```julia
m = ModelDef(...)
r = McsRunner(m)

# Optionally redefine random variables, overriding defaults
setrv!(r, :rvname1, Normal(10, 3))

# Optionally assign an alternative RV or distribution to a model parameter
setparam!(r, :comp1, :param1, :rvname1)
setparam!(r, :comp1, :param2, Uniform(0, 3))

# Adjust what should be saved per model run
@add_results(r, param10, param11[US])
@drop_results(r, param1, param1)

# Run trials 1-5000. Also can specify as a range (e.g., 5001:10000) 
# or vector of trial numbers.
run!(r, 5000)

# Save results to the indicated directory
write(r, dirname)
```

### Saving ensemble results

Ensemble results will be stored in an object of type ```EnsembleResult```, which is used by all subtypes of ```ModelRunner```. By default, only model outputs tagged for output will be saved in the ```EnsembleResult``` instance. Parameters can be identified as "output" parameters in ```@defmcs```, and/or directly in a concrete subtype of ```ModelRunner```, in which case default values set in ```@defmcs``` can be overridden.

The method ```write(r::ModelRunner, dirname::String)``` will save model results to set of CSV files in the named directory. Initially, we will store the data in [tidy](https://cran.r-project.org/web/packages/tidyr/vignettes/tidy-data.html) format in which variables are in columns and each row represents an observation. This format is verbose but flexible and commonly used by consumers of data such as plotting packages. Other formats can be generated if the need arises.

In the initial implementation, results will be saved as follows:

* Scalar model results will be written to ```"$dirname/scalars.csv"```. The file will have a column holding the trial number, and one column labeled with each parameter name. Each row in the file will contain all scalar parameter values data for a single  model run. 
  * Example:

    | trialnum | foo | bar | baz | ... |
    | -------- | --- | --- | --- | --- |
    | 1        | 1.6 | 0.4 | 110 | ... |

  * Alternatively, we could flatten to 3 columns: 

    | trialnum | paramname | value |
    | -------- | --------- | ----- |
    | 1        | foo       | 1.6   |
    | 1        | bar       | 0.4   |
    | 1        | baz       | 110   |

* All model results with a single time dimension will be written to ```"$dirname/timeseries.csv"```, with columns:

  | trialnum | year | foo   | bar  | baz  | ... |
  | -------- | ---- | ----- | ---- | ---- | --- |
  | 1        | 2010 | 100.6 | 41.6 | 9.1  | ... |
  | 1        | 2015 | 101.7 | 44.5 | 10.2 | ... |
  | 1        | 2020 | 102.8 | 50.1 | 12.4 | ... |
  | ...      | ...  | ...   | ...  | ...  | ... |
  | 2        | 2010 | 101.6 | 43.7 | 10.4 | ... |
  | 2        | 2015 | 102.4 | 60.1 | 21.3 | ... |
  | 2        | 2020 | 105.7 | 55.3 | 14.2 | ... |
  | ...      | ...  | ...   | ...  | ...  | ... |

  * As with scalar results, this might be flattened further to:

    | trialnum | paramname | year | value |
    | -------- | --------- | ---- | ----- |
    | 1        | foo       | 2010 | 100.6 |
    | 1        | foo       | 2015 | 101.7 |
    | 1        | foo       | 2020 | 102.8 |
    | ...      | ...       | ...  | ...   |
    | 2        | foo       | 2010 | 101.6 |
    | 2        | foo       | 2015 | 102.4 |
    | 2        | foo       | 2020 | 105.7 |
    | ...      | ...       | ...  | ...   |

  * Another alternative would be to store each timeseries result to its own CSV file, in which case the second (flattened) format would be used, minus the "paramname" column, which would be implicit from the filename. This would be more consistent with the matrix format below, since a timeseries result is just a matrix result with only one dimension.

* Matrix results will be saved to individual files named ```"$dirname/$paramname.csv"```. Matrices will be flattened so that each dimension appears as a column. For example, a matrix with dimensions "time" and "region" will have columns "trialnum", 

  | trialnum | region | year | value |
  | -------- | ------ | ---- | ----- |
  | 1 | US | 2010 | 1.1 |
  | 1 | US | 2015 | 1.9 |
  | ...|
  | 1 | CHI | 2010 | 0.2 |
  | 1 | CHI | 2015 | 0.8 |
  | ...|
  
* Another option for saving ensemble outputs might include writing to any "sink" type that accepts named tuples.
